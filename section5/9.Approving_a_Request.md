# 124. Approving a Request

>requests 不再是数组格式而是map
![img](../image/section5/26.png ':size=600')
 
## approveReauest投票请求
1. 判断投票人已经捐钱
2. 判断投票人没有投过票
   
```javascript
    // 投票
    function approveReauest(uint index) public {
        Request storage request =  requestsMap[index];
       // storage is very important because we want to manipulate the copy of the struct that exists inside of storage.
       
        require(approvers[msg.sender]); // 是否已经投钱
        require(!request.approvals[msg.sender]); //是否已经投票
        
        request.approvals[msg.sender] = true;
        request.approvalCount++;
        
    }
```



## remix 测试
![img](../image/section5/26.png ':size=800')



## manager转账 finalizerequest

1. 当有足够多的人投票统一后，manager需要获得可以转钱的权利

```javascript
    // 执行请求
    function finalizerequest(uint index) public restricted {
        
        Request storage request =  requestsMap[index]; // 创建变量
        // complete 不是true
        require(!request.complete);
        request.complete = true;
        
    }
```


## 如何确定已获得足够的投票
1. 超过50%的人投票可以时候才可以执行这项提议，所以需要比较这项投票同意的人数和总人数
2. If we want to get some count of the total number of people who exist inside this mapping 
3. we have to actually create a second variable or another state variable to keep track of how many people have joined in this contract.

```javascript
    //address payable[] public approvers;
    mapping(address => bool) public approvers;
    uint public approversCount; // 参与人数
		
    // 执行请求
    function finalizerequest(uint index) public restricted {
        
        Request storage request =  requestsMap[index]; // 创建变量
        require(request.approvalCount > (approversCount / 2)); // 大于一半
        // complete 不是true
        require(!request.complete);
        
        // Remember this is the address of the person that we want to send our money to 
        request.recipient.transfer(request.value);
        request.complete = true;
        
    }

```


## 完整

```javascript
// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.7.0 <=0.8.0;

/** 
 * @title Ballot
 * @dev Implements voting process along with vote delegation
 */

contract Campaign {
    struct Request {
        string description; //注意是分号
        uint value;
        address  payable recipient;
        bool complete;
        uint approvalCount;
        mapping(address => bool) approvals; // 记录投票人数
        
    }
    uint numRequests = 0;
    mapping (uint => Request) public requestsMap;
    
    
    // public private
    address payable public manager;
    uint public minimumContribution;
    
    //address payable[] public approvers;
    mapping(address => bool) public approvers;
    uint public approversCount; // 参与人数
    
    modifier restricted() {
        require(msg.sender == manager); // 确保只有manager可以操作
        _;
    }
    
    constructor(uint minimum) {
        //  the sender property describes exactly who is attempting to create the contract.
        manager = msg.sender;
        minimumContribution = minimum;
    }
    
    //So payable right here is what makes this function able to receive some amount of money.
    function contribute() public payable {
        //判断是否满足最低标准
        require(msg.value > minimumContribution);
        // 将地址加入mapping,值设置为true,address
        approvers[msg.sender] = true;
        approversCount++; // 人数增加
        
    }
    // 创建请求
    function createRequest(string calldata description, uint value, address payable recipient) public restricted {
        Request storage newRequest = requestsMap[numRequests++];
        newRequest.description = description;
        newRequest.value = value;
        newRequest.recipient = recipient;
        
    }
    // 投票
    function approveReauest(uint index) public {
        Request storage request =  requestsMap[index];
       // storage is very important because we want to manipulate the copy of the struct that exists inside of storage.
       
        require(approvers[msg.sender]); // 是否已经投钱
        require(!request.approvals[msg.sender]); //是否已经投票
        
        request.approvals[msg.sender] = true;
        request.approvalCount++;
        
    }
    
    // 执行请求
    function finalizerequest(uint index) public restricted {
        
        Request storage request =  requestsMap[index]; // 创建变量
        require(request.approvalCount > (approversCount / 2)); // 大于一半
        // complete 不是true
        require(!request.complete);
        
        // Remember this is the address of the person that we want to send our money to 
        request.recipient.transfer(request.value);
        request.complete = true;
        
    }
    
    
}
```